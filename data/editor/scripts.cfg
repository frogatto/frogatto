{
script: [
	{
		id: "Simple World",
		script: "def add_enemy(left, right, ypos)
   add_object(enemy[0], left + (1d max(1, (right - left) - enemy[1])), ypos - enemy[2], -1)
   where enemy = choose(
    [
	  ['ant_black', 40, 60],
	  ['ant_black', 40, 60],
	  ['ant_black', 40, 60],
	  ['ant_red', 40, 60],
	  ['ant_red', 40, 60],
	  ['ant_flying_black', 40, 150],
	  ['ant_flying_black', 40, 190],
	  ['ant_flying_black', 40, 240]
	]
   )
;
def add_prop(left, right, ypos)
   add_object(prop[0], left + (1d max(1, (right - left) - prop[1])), ypos - prop[2]*2, 1)
   where prop = choose(
    [
     ['tree-world1', 80, 120],
     ['tree-world1-2', 80, 120],
     ['tree-world1-3', 80, 120]
	]
   )
;
def prettify_platform(left, right, ypos)
  map([0] * 1d((right - left)/32), add_prop(left, right, ypos))
;

def add_enemies_to_platform(left, right, ypos)
  map([0] * 1d((right - left)/128), add_enemy(left, right, ypos))
;


def generate_heights(tile_id, left, top, right, bottom, last_height)
  if(left < right and height < bottom,
     [prettify_platform(left*32, min(right, left + platform_length)*32, height*32),
      add_enemies_to_platform(left*32, min(right, left + platform_length)*32, height*32),
	  add_tiles(tile_id, left, height, min(right, left + platform_length), bottom),
	  generate_heights(tile_id, left + platform_length, top, right, bottom, height)])
		  where platform_length = 2d6, height = last_height + (1d5 - 1d5);

      generate_heights('nrk', left, top, right, bottom, (top+bottom)/2)
         where top = min(map(cells, y)), bottom = max(map(cells, y)),
		       left = min(map(cells, x)), right = max(map(cells, x))",
	},
	{
		id: "Decorate Arcade Solid Tiles",
		script: "[map(cells, if(find(tiles, 'tile', tile = 'agr') and 1d4=4, add_tiles('act', x, y))),
map(cells, if(find(down.tiles, 'tile', tile = 'agr') and
             (not find(tiles, 'tile', tile = 'agr')) and
			 (not find(left.tiles, 'tile', tile = 'agr')) and
			 (not find(right.tiles, 'tile', tile = 'agr') and 1d4=4),
			    add_tiles('ags', x, y)))
]",
	},
	{
		id: "Add Grass",
		script: "map(cells, if(find(down.tiles, 'tile', tile = 'nrk') and
             (not find(tiles, 'tile', tile = 'nrk')) and
			 (not find(left.tiles, 'tile', tile = 'nrk')) and
			 (not find(right.tiles, 'tile', tile = 'nrk')),
			    add_tiles('ngs', x, y)))",
	},
	{
		id: "Smooth Rocks",
		script: "#function which counts the gray rocks in a column. 'cell' is the cell at
 the top of the column, and 'count' is the number of cells in the column.#
def count_nrk_in_column(cell, count)
  if(count <= 0, 0, if('nrk' in cell.tiles, 1, 0) + count_nrk_in_column(cell.down, count-1));

#function which counts the gray rocks in a rectangle. 'cell' is the cell
 at the top-left of the rect, and 'nrows' and 'ncols' give the size of the rect#
def count_nrk_in_rect(cell, nrows, ncols)
  if(ncols <= 0, 0, count_nrk_in_column(cell, nrows) + count_nrk_in_rect(cell.right, nrows, ncols-1));

#for all cells with 'nrk' in them, calculate the number of nrk tiles in a
 3 tile radius. If this number is greater than 30, leave the nrk there. If
 it's greater than 15, then make leaving the nrk there random; otherwise clear
 the nrk.#
map(filter(cells, 'nrk' in tiles), 'cell',
  if(count < 15 or count < 30 and 1d2 = 1, remove_tiles('nrk', cell.x, cell.y)) where count = count_nrk_in_rect(cell.left.left.left.up.up.up, 7, 7))",
	},
	{
		id: "Round Grey Rocks",
		script: "#function which counts the gray rocks in a column. 'cell' is the cell at
 the top of the column, and 'count' is the number of cells in the column.#
def count_nrk_in_column(cell, count)
  if(count <= 0, 0, if('nrk' in cell.tiles, 1, 0) + count_nrk_in_column(cell.down, count-1));

#function which counts the gray rocks in a rectangle. 'cell' is the cell
 at the top-left of the rect, and 'nrows' and 'ncols' give the size of the rect#
def count_nrk_in_rect(cell, nrows, ncols)
  if(ncols <= 0, 0, count_nrk_in_column(cell, nrows) + count_nrk_in_rect(cell.right, nrows, ncols-1));

#for all cells with nothing in them, calculate the number of nrk tiles in a
 4 tile radius. Do the opposite of below to round in corners.#
map(filter(cells, not ('nrk' in tiles)), 'cell',
  if(count > 40 or count > 45 and 1d2 = 1, add_tiles('nrk', cell.x, cell.y)) where count = count_nrk_in_rect(cell.left.left.left.left.up.up.up.up, 9, 9)) +

#for all cells with 'nrk' in them, calculate the number of nrk tiles in a
 4 tile radius. If this number is greater than 30, leave the nrk there. If
 it's greater than 15, then make leaving the nrk there random; otherwise clear
 the nrk.#
map(filter(cells, 'nrk' in tiles), 'cell',
  if(count < 30 or count < 35 and 1d2 = 1, remove_tiles('nrk', cell.x, cell.y)) where count = count_nrk_in_rect(cell.left.left.left.left.up.up.up.up, 9, 9))",
	},
],
}