/*
 this should be one of two primary enemy prototypes; essentially we have a divide between "enemies which can move", and "enemies which are permanently fixed in one location".  Those which can move have a bunch of extra baggage in order to handle physical collision, those which don't are much more lightweight.
 
 The key thing about this prototype is providing the "feel" of the average enemy in the game; providing a bunch of stock behaviors governing how they bounce off walls, floors and ceilings, etc, so that all mobile enemies feel like they're governed by consistent physical laws.
 
 We bake in two common assumptions to this prototype; we have the hooks for swallowability (though it can easily be turned off) since most mobile enemies are swallow/throwable.  We also have the hooks for springiness/bounceability.
 
 Required animations:  [default_anim, 'turn']
 Optional animations:  ['spring', 'hurt']
*/


{
id: "movable_enemy",
prototype: ["hittable"],
mass: 5,
friction: 1000,
traction: 1000,
activation_border: 1000,

hitpoints: 50,
solid_area: [5,10,20,25],
solid_dimensions: ["enemy","common"],

properties: {
	swallowable: "bool :: true",
	team: "'evil'",
	hurt_velocity_y: "int :: -400",
	hurt_velocity_x: "int :: 200",
	attack_damage: "int :: if(not underwater, if(higher_difficulty, 2, 1), 0)",
	taxonomy: "string :: 'bug'",
	is_enemy_character: true,
	acquirable_item_drop_value: "int :: 10",					
	points_value: "int :: 10",
	
	default_anim: "string :: 'stand'",		# often replaced for enemies like fliers which don't 'stand'
											#TODO: check if this is safe to replace with 'walk' in subsequent prototypes like ant; we want 'stand' in the base, but aggressive walkers may not flinch and will just keep walking


	feet_are_bouncy: "bool :: true",

	basic_type: "string :: me.type",
	thrown_type: "string :: me.basic_type+'.thrown'", 
	frogourmet_tag: "me.basic_type",
	springiness: "int :: 190",
	spring_amount: "int :: 1200",
	bounce_roundness: "int :: 70",
	default_solid_dimensions: "[string] :: ['enemy','common']",

	enter_water: "[	
					splash_effect(),
					handle_death({damage_type: 'neutral'}),
					die()
				]",
#-------------------------- turning logic --------------------------#
	turn_cooldown: "int :: 30",

	/*
		this is used for periodic checks like testing bounds; it finds out if we're nearing 'soft' boundaries and *want* to start turning.  It doesn't check if we are actually *allowed* to; the hard limitations like the fraction-of-a-second cooldown on turning (and any "don't enter the animation because we're already in it" checks) get checked inside the actual turn_around command itself.  It just checks to see if we want to.
	
		By default it's virtual inside movable_enemy, since walkers and fliers have such different criterion.
	*/
	check_if_we_should_turn_and_do_so_if_appropriate: "commands :: null", 

	turn_around: "commands :: if(cycle-_last_turned > turn_cooldown, [set(_last_turned,cycle),set(facing, -facing),animation('turn')])",
	//turn_towards_player: "commands :: if(cycle-_last_turned > turn_cooldown and facing != direction_towards_player, animation('turn'))",

	try_facing: "def(int direction) -> commands if(direction != facing,turn_around)",
	
#-------------------------- vars --------------------------#
	_last_turned: { type: "int", default: 0 },
},


editor_info: {
	category: "enemies, general",
},

#-------------------------- setup --------------------------#
on_create: "animation(default_anim)",

#-------------------------- collision handling --------------------------#
	//for collisions against solid areas; if we're hitting something that isn't obj hittable, it should be treated like terrain.  Generally terrain, at least for feet, is going to be much more "grippy"; essentially for gameplay reasons we want "platform-like" objects to be grippable, and most non-platforms to be bouncy by default
	//because we don't have a way to feed terrain "surface normals" into elastic_collision, we're basically special-casing the responses for terrain based on the side of the object struck.  If we do at some point pursue that, those essentially would replace the "angle to other object's center" in the calcs.
on_collide_side: "	if(collide_with is obj hittable, 
						elastic_collision(collide_with, {multiplier: 0.7, constraint: {min: 50, max: 1000}}),
						if(not is_standing, if(abs(velocity_x) > 200, set(velocity_x, -velocity_x*(1d4*0.1)), set(velocity_x, 0)))
					) where collide_with = arg.collide_with",
					
on_collide_head: "	if(collide_with is obj hittable, 
						elastic_collision(collide_with, {multiplier: 0.7, constraint: {min: 50, max: 1000}}),
						set(velocity_y, -velocity_y/2)
					) where collide_with = arg.collide_with",
					
on_collide_feet: "	if(collide_with is obj hittable, 
						elastic_collision(collide_with, {multiplier: 1.1, constraint: {min: 50, max: 1000}}),
						if(abs(velocity_y) > 200 and feet_are_bouncy, [attempt_animation('spring', default_anim),set(velocity_y, -velocity_y/4)], set(velocity_y, 0))
					) where collide_with = arg.collide_with",

on_bounced_on: "[sound('bug-bounce.wav'),attempt_animation('spring', default_anim)]",
on_collide_damage: "[handle_death({damage_type: 'neutral'}),die()]",
on_force_death: "[handle_death({damage_type: 'neutral'}),die()]",

#-------------------------- animation switching --------------------------#

on_end_turn_anim: "animation(default_anim)",
on_end_hurt_anim: "animation(default_anim)",
on_stuck: "if(not solid(level, midpoint_x, y+self.img_h-10), add(velocity_y, -1200), add(velocity_x, 1d600-300))",
}