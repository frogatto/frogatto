{
id: "periodic_mook_spawner",
hidden_in_game: true,
is_strict: true,
prototype: ["standard_values"],

properties: {
		/*
			The object will be 'held' in the spawner for a fixed period of time, and then it will get launched out (either down, or up, as appropriate).
		*/
	release_countdown_length: "int :: 150",

	zone_width: "abs(_x2_bound - _x_bound)",
	zone_height: "abs(_y2_bound - _y_bound)",
	act_B: "max(zone_width, zone_height)*2",


#-------------------------- things we must manually override --------------------------#
		/*
			This isn't everything - you'll also need to tweak some behavior settings for cosmetic 'does it release up or down' stuff, and how fast - however, this is a bit unusual:
		
			Because these variables directly deal with what type of object is being spawned, and because we can't access local properties in init statement or editor_info fields, these two fields have to be manually overridden when you're writing an object that inherits from this prototype.
		*/

		//	This one needs an init statement.
	_mook_tracker:	{ type: "class mook_spawning_tracker", init: "construct('mook_spawning_tracker',{this_obj: me, spawnee_types: ['ant_black'], max_objects: 3})" },
		//	This one needs editor-info values.
	spawn_type: { type: "string", default: "'ant_black'" },


#-------------------------- temporary vars --------------------------#
	_spawning: { type: "obj hittable|null", default: null, persistent: false },
	_release_counter: { type: "int", default: 0, persistent: false },


#-------------------------- editor vars --------------------------#
	max_objects: { type: "int", default: 3, editor_info: { type: "int", value: "3" }  },

	_x_bound: { type: "int", editor_info: { type: "x", value: "mid_x -300" } },
	_x2_bound: { type: "int", editor_info: { type: "x", value: "mid_x+300" } },
	_y_bound: { type: "int", editor_info: { type: "y", value: "mid_y-300" } },
	_y2_bound: { type: "int", editor_info: { type: "y", value: "mid_y+300" } },
	

#-------------------------- behavior settings --------------------------#
	play_spawn_sound: "sound_falloff('ant-spawn.wav')",
	zorder_whilst_spawning: "@include data/zorder.cfg:exterior_near_background",
	spawn_vel: "{x: int, y: int} :: {x: 0, y: -800}",
	spawn_offset: "{x: int, y: int} :: {x: 0, y: 0}",

#-------------------------- behavior --------------------------#
	create_and_hold_child: "commands :: if(_spawning = null, [
			play_spawn_sound,
			set(the_child.animation, 'stand'),
			set(_spawning, (obj hittable <- the_child)),
			add_object(the_child),
		] where the_child = object(spawn_type,
			mid_x + spawn_offset.x,
			mid_y + spawn_offset.y,
		{
			parent: me,
			collides_with_level: false,
			solid_dimensions_in: [],
			zorder:	zorder_whilst_spawning,
		})
	)",
	
	release_child: "commands :: if(_spawning != null, [
		set(_spawning.parent, null),
		set(_spawning.collides_with_level, true),
		set(_spawning, null),
		set(_spawning.velocity_x, spawn_vel.x),
		set(_spawning.velocity_y, spawn_vel.y),
		set(_spawning.zorder,0),
		set(_spawning.solid_dimensions_in, _spawning.default_solid_dimensions),
		set(_release_counter, 0)
	])",
	
	

},

on_create: "[
				set(activation_border, act_B),
				set(_mook_tracker._x_bound, _x_bound),
				set(_mook_tracker._x2_bound, _x2_bound),
				set(_mook_tracker._y_bound, _y_bound),
				set(_mook_tracker._y2_bound, _y2_bound),
				set(_mook_tracker.max_objects, max_objects)
			]", 

on_process: "[
		if(_spawning, set(_spawning.accel_y, 0)),
		if(_mook_tracker.should_create_a_new_mook or _spawning != null, add(_release_counter, 1), set(_release_counter, 0)),
		if(_release_counter >= (0.75 * release_countdown_length), create_and_hold_child ),
		if(_release_counter >= (1.00 * release_countdown_length), release_child)
	]",

}
