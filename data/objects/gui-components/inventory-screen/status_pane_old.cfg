{
id: "status_pane",
prototype: ["inventory_screen_pane"],

properties: {
#-------------------------- input handling --------------------------#

		handle_dispatched_input: "def(InputValue input) -> commands [
			switch(input,
				enum arrow_left,	status_panel_active_selector.select(status_panel_active_selector.next_item(4)),
				enum arrow_right,	status_panel_active_selector.select(status_panel_active_selector.next_item(6)),
				enum arrow_up,		status_panel_active_selector.select(status_panel_active_selector.next_item(8)),
				enum arrow_down,	status_panel_active_selector.select(status_panel_active_selector.next_item(2)),
		
				enum trigger_1, 	status_panel_active_selector.activate()
		)]",
		

#-------------------------- core behavior --------------------------#
	initialize_parts: "commands :: [
		set(status_panel_active_selector, status_panel_primary_selector),
		set_widgets(
			me,
			{
				//Frogatto image.
				type: 'image',
				image: if(frogatto.swallowed_object, 'characters/fat-frogatto-spritesheet1.png', 'characters/frogatto-spritesheet1.png'),
				area: [2,89,33,121],
				image_width: (33-2)*4,
				image_height: (121-89)*4,
				x: 370+24, y: 25+8,
			},

			if(frogatto.swallowed_object, {
				//Frogatto belly contents.
				type: 'animation',
				object: frogatto.swallowed_object.type, //frogatto.type works just as well, here.
				name: 'on_back',
				x: 435+24, y: 120+8,
			}),

			//Trophy display
			/*
			map(
				filter(
					[
						{kind: 'ant',          rect: [0,0,31,31]}, //Image rects, from trophy.cfg
						{kind: 'kitty',        rect: [32,0,63,31]},
						{kind: 'bunny',        rect: [64,0,95,31]},
						{kind: 'milgramblock', rect: [96,0,127,31]},
						{kind: 'fumper',       rect: [0,32,31,63]},
						{kind: 'bat',          rect: [32,32,63,63]},
						{kind: 'fish',         rect: [64,32,95,63]},
						{kind: 'squirrel',     rect: [96,32,127,63]}
					],
					frogatto.get_stored_event('trophy_' + value.kind + '_acquired')
				),
				{
					//Trophy icons
					type: 'image',
					image: 'props/trophies.png',
					area: value.rect,
					x: 36 + index * 58,
					y: 435,
				}
			),
			*/
		),

/*		
		map(filter(keys(frogatto.items_carried), value != 'tongue'), 'item_name', 
			add_object(object('${me.type}.status_pane_item_display', 0, 0, 1, {
				animation: item_name,
				count: frogatto.number_of_item(item_name),
				parent: me,
				relative_x: -183+72*positions[index][0],
				relative_y: -155+72*positions[index][1],
			}))
		) where positions = [[item_x,item_y] | item_x <- range(4), item_y <- range(4)],
		
		[
			add_object(txt),
			set(txt.parent, me),
			set(txt.relative_x, -80),
			set(txt.relative_y, 135),
		] where txt = status_panel_text_display,
		
		map(frogatto.ability_list, 'ability_name',
			add_object(object('${me.type}.status_pane_ability_display', 0, 0, 1, {
				animation: ability_name + if(frogatto.current_ability_name = ability_name, '_active', ''),
				parent: me,
				relative_x: 150 + 16.5*(int :: [0,4,0,4,0,4][index]),
				relative_y: -32 + 17.0*(int :: [0,0,4,4,8,8][index]),
			}))),

		[
			add_object(ps),
			set(ps.parent, me),
			set(ps.relative_x, -80),
			set(ps.relative_y, 135),
		] where ps = status_panel_primary_selector,
*/
	]",



	status_panel_text_display: {
		type: "obj text_controller",
		init: "object('text_controller', 0,0,1, {
			use_absolute_screen_coordinates: true,
			zorder: lib.zorder.get('overlaid_gui_dialog_gui_element'),
			txt: ~inventory~,
			align: 'center',
		})",
	},
	
	status_panel_primary_selector: {
		type: "obj status_pane.status_panel_selector",
		init: "object('status_pane.status_panel_selector', 0,0,1, {
			use_absolute_screen_coordinates: true,
			zorder: lib.zorder.get('overlaid_gui_dialog_gui_element'),
		})",
	},
	
	
	status_panel_active_selector: {
		type: "obj status_pane.status_panel_selector", 
		get: "_data",
		set: "set(_data, value)",
		dynamic_initialization: true,
	},


},



animation: {
	id: "normal",
	image: 'gui/pause-menu/panel-background-stone.png',
	x:0, y:0, w:276, h:264,
},



object_type: [
	{
		"@base": true,
		use_absolute_screen_coordinates: true,
	},

	{
		id: "status_pane_item_display",
		zorder: "@include data/zorder.cfg:overlaid_gui_dialog_gui_element",
		
		properties: {
			count: {type: 'int'},
		},
		
		on_create: "if(count > 1, text('\n\n\n\n\n\n\n\n\nx${count}', 'white_outline', 2, 'right'))",
		
		on_mouse_move: "parent.status_panel_text_display.set_text(item_name) asserting parent is obj status_pane asserting parent asserting item_name is string
			where item_name = {
				acquirable_attractor: object('buyable_acquirable_attractor'),
				greater_mana_talisman: object('buyable_greater_mana_talisman'),
				lesser_mana_talisman: object('buyable_lesser_mana_talisman'),
				tongue_extension: object('buyable_tongue_extension'),
			}[animation].short_item_name",
		
		animation: [{
			"@base": true,
			image: "props/store-items.png",
			frames: 1,
			scale: 4,
		},{
			id: "acquirable_attractor",
			rect: [1,247,17,262],
		},{
			id: "greater_mana_talisman",
			rect: [1,111,17,126],
		},{
			id: "lesser_mana_talisman",
			rect: [1,94,17,109],
		},{
			id: "tongue_extension",
			rect: [1,179,17,195],
		},],
	},

	{
		id: "status_pane_ability_display",
		zorder: "@include data/zorder.cfg:overlaid_gui_dialog_gui_element",
		
		properties: {
			frogatto: "obj frogatto_playable <- level.player",
		
			displayed_ability: "{
				tongue: 'tongue',
				firebreath: 'firebreath',
				energyshot: 'energyshot',
				homingshot: 'homingshot',
				acidbreath: 'acidbreath',
				tongue_active: 'tongue',
				firebreath_active: 'firebreath',
				energyshot_active: 'energyshot',
				homingshot_active: 'homingshot',
				acidbreath_active: 'acidbreath',
			}[animation]",
		},
		
		on_mouse_move: "parent.status_panel_text_display.set_text(item_name) asserting parent is obj status_pane asserting parent asserting item_name is string
			where item_name = {
				tongue: if(not frogatto.swallowed_object, ~tongue~, ~spit~),
				firebreath: if(not super, ~fire breath~, ~fireball~),
				energyshot: if(not super, ~energy shot~, ~energy blast~),
				homingshot: if(not super, ~homing shot~, ~orbit shield~),
				acidbreath: if(not super, ~acid ball~,   ~acid wave~),
			}[displayed_ability] 
			where super = frogatto.stomach_contents_allow_super_attack,
		",
		
		on_mouse_down: "[ //on_click doesn't have enough dead zone to be usable.
			//set(frogatto.current_ability_name, displayed_ability),
			
			map(filter(level.active_chars, value.type = me.type), 'ability_display',
				fire_event(ability_display, 'deactivate_graphic')),
			fire_event(me, 'activate_graphic'),
		]",
		
		on_deactivate_graphic: "set(animation, displayed_ability)",
		on_activate_graphic: "set(animation, displayed_ability + '_active')",
		
		animation: [{
			"@base": true,
			image: "props/store-items.png",
			frames: 1,
			scale: 3,
		},{
			id: "tongue",
			rect: [121,179,137,194],
		},{
			id: "firebreath",
			rect: [121,196,137,211],
		},{
			id: "energyshot",
			rect: [121,145,137,160],
		},{
			id: "homingshot",
			rect: [121,213,137,228],
		},{
			id: "acidbreath",
			rect: [121,230,137,245],
		},{
			id: "tongue_active",
			rect: [001,179,017,194],
		},{
			id: "firebreath_active",
			rect: [001,196,017,211],
		},{
			id: "energyshot_active",
			rect: [001,145,017,160],
		},{
			id: "homingshot_active",
			rect: [001,213,017,228],
		},{
			id: "acidbreath_active",
			rect: [001,230,017,245],
		},],
	},

	{
		id: "status_panel_selector",
		zorder: "@include data/zorder.cfg:in_front_of_everything",
		
		properties: {
			cursor_offset: "[0,0]",
			selected_object: {type: "custom_obj|null", default: null},
			
			fake_mouse_event_args: { // Needed, can't fire event without it.
				type: "{mouse_button: int, mouse_x: int, mouse_y: int, world_point: [decimal,decimal,decimal,]}",
				default: {mouse_button: -1, mouse_x: 0, mouse_y: 0, world_point: [0,0,0]},
			},
			
			item_chars: "[custom_obj] <- 
				filter(level.active_chars, 
					   value is obj status_pane.status_pane_ability_display 
					or value is obj status_pane.status_pane_item_display
				)",
			
			next_item: "def(int direction) -> custom_obj|null //as on a number pad
				choose(
					filter(item_chars,
						switch(direction,
							2, value.y > y2 and abs(decimal(x2-value.x)/decimal(y2-value.y)) <= 2.0, //2.0 and 0.5 are the slope values for which the objects we want will be above/below. The slope conceptually starts at the object we're looking at, and is a V shape because of abs(). Originally, with slope values of 1, we would only consider objects that were in a 45Â° cone in the direction we wanted to move the cursor. However, that turned out to be a bit janky-feeling when we moved from one arm of an L-shape to another, so we widened the search angle and then distorted the coordinates of the closeness function to match.
							4, value.x < x2 and abs(decimal(x2-value.x)/decimal(y2-value.y)) >= 0.5, //The initial </> comparison is a workaround a infinite math. the abs() is returning the absolute of -inf8, which is one more than inf7, so it wraps back around to -inf8. However, -1/0 is inverting inf7, coming out with -inf7, which is one more than -inf8 afaict.
							6, value.x > x2 and abs(decimal(x2-value.x)/decimal(y2-value.y)) >= 0.5,
							8, value.y < y2 and abs(decimal(x2-value.x)/decimal(y2-value.y)) <= 2.0,
						) and if(selected_object, value.animation != selected_object.animation, true)),
					-lib.math.length(
						zip(scale, value.mid_xy, a*b), 
						[(midpoint_x - cursor_offset[0]) * scale[0], (midpoint_y - cursor_offset[1]) * scale[1]]
					) 
				) where x2 = x - self.cursor_offset[0],
				        y2 = y - self.cursor_offset[1],
				        scale = [if(direction in [2,8], 1, 2), //Weight search to favour items closer in the direction we want. The green is the scaling after the *2 we apply here (refer to the external image: status_pane_comment.png
				                 if(direction in [4,6], 1, 2)]",
					
			select: "def(custom_obj|null target) -> commands if(target, [
				set(selected_object, target),
				set(relative_x, target.relative_x), set(relative_y, target.relative_y),
				fire_event(target, 'mouse_move', fake_mouse_event_args),
			])",
			
			activate: "def() -> commands if(selected_object, [
				fire_event(selected_object, 'mouse_down', fake_mouse_event_args),
				fire_event(selected_object, 'mouse_up', fake_mouse_event_args),
				fire_event(selected_object, 'click', fake_mouse_event_args),
			])",
		},
		
		on_create: "if(first_target, 
			select(first_target), 
			schedule(1, fire_event('create'))
		) where first_target = find(level.active_chars, 
			value is obj status_pane.status_pane_item_display)",
		
		animation: [{
			"@base": true,
			image: "gui/pause-menu/cursor.png",
			frames: 1,
		},{
			id: "normal",
			rect: [63,0,125,62],
		},],
	},

],
}
