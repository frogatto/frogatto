{
	id: "hud_gold_display",
	prototype: ["hud_component"],
	hidden_in_game: true,
	

	properties: {
		set_text: "def(string text) -> commands [set(txt, text), fire_event(me, 'create')]",
		txt: {type: "string", default: "+"},
		size: "2",
		font: "string :: 'numbers_gold_enormous'",
		align: "'right'",
		
		
		comparison_point_for_coins: { type: "int", default: 0 },
		last_changed_coins: { type: "int", default: -500 },
		display_timeout: "decimal :: 100",
		
		vertical_full_offset: "decimal :: 90",
		vertical_offset: "vertical_full_offset * lib.math.ease_in_out_elastic(ratio)
		
			where ratio = (1.0 - min(1.0, decimal(me.cycle -  (last_changed_coins + delay) )/display_timeout ))
			where delay = 200",
		
		poll_for_coin_changes: "if(comparison_point_for_coins != level.player.coins,
									[
										set(comparison_point_for_coins, level.player.coins),
										set(last_changed_coins, me.cycle),
									]
								)",
								
		native_position: "{x: decimal, y: decimal} :: {x: screen_w - 50*2, y: -50} where screen_w = level.camera_position[2]",

		set_vertical_pos: "commands :: set(mid_y, native_position.y + vertical_offset)",
		
		coin_icon: { type: "obj hud_gold_display.coin_icon", init: "object('hud_gold_display.coin_icon', x, y, {parent: me})", },
	},

	on_create: "[text(txt, font, size, align), add_object(coin_icon), set(coin_icon.relative_x, 60), set(coin_icon.relative_y, 8)]",

	on_window_resize: "set(xy, [new_pos.x, new_pos.y])
		where new_pos = {x:screen_w - 66*2,y:50}
		where screen_w = level.camera_position[2]",


	//Sync UI state with Frogatto.
	on_process: "[
		if(level.player is obj frogatto_playable, [
			me.set_text( str(level.player.coins) + 'Ã—')
			;
			set(text_alpha, alpha_proxy),
			poll_for_coin_changes,
			set_vertical_pos,
		])
	]",
	
	
	object_type: {
		id: "coin_icon",
		prototype: ["hud_component"],

		
		on_end_anim: "animation('normal')",
		
		animation: {
			no_remove_alpha_borders: true,
			id: "normal",
			image: "props/items.png",
			rect: [4,141,37,174],
			pad: 3,
			frames: 6,
			duration: 6,
		},
	}
}