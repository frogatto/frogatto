//Show the currently selected ability, such as 'tongue' or 'firebreath'.
{
	id: "ability_secondary_display",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",

	properties: {
		player: "level.player",
		


#-------------------------- components --------------------------#
		graphic: {
			type: "custom_obj",  
			init: "object('${me.type}.graphic',0,0,1, {zsub_order: -1})",
		},
		

		graphic_post1: {
			type: "custom_obj",  
			init: "object('${me.type}.graphic',0,0,1)",
		},

		graphic_post2: {
			type: "custom_obj",  
			init: "object('${me.type}.graphic',0,0,1)",
		},

		graphic_pre1: {
			type: "custom_obj",  
			init: "object('${me.type}.graphic',0,0,1)",
		},

		graphic_pre2: {
			type: "custom_obj",  
			init: "object('${me.type}.graphic',0,0,1)",
		},

		overlay: {
			type: "custom_obj",  
			init: "object('${me.type}.overlay',0,0,1)",
		},


#-------------------------- core behavior --------------------------#
		graphic_cells: "[custom_obj] ::
							[graphic_pre2, graphic_pre1, graphic, graphic_post1, graphic_post2]",

		set_to_animation: "def( custom_obj target, [int,int,int,int] rect) -> commands
			set(target.animation, {
				image: 'gui/hud4.png',
				duration: -1,
				id: 'normal',
				rect: rect
			},
		)",

		has_just_changed: "bool :: player.last_toggled_secondary_item = player.cycle - 1",  //We're putting this a frame in the past because I'm not sure we can consistently rely on the player object to execute before this object, and if we were ever in a situation where that happened, the flag wouldn't be set by the time we checked this (and the player's cycle would probably be updated even before its processing, so effectively this would get "missed".  Maybe - it being a bug is contingent on that second thing actually being true, but this way there's no bug either way.

		do_carousel_anim: "commands :: 
			map(graphic_cells, 
				[
					//debug(cycle),
					set(value.alpha, 255),
					set(value.mid_x, mid_x  + 90*index),
					;
					animate(value, {
							mid_x: mid_x  + 90*index,
							mid_y: mid_y ,
						},{
							duration: 60,
							name: 'hud_item_carousel',
							replace_existing: true,
							on_complete: anim2
					}) where anim2 = animate(value, {
							alpha: if(index = 2, 255, 0),
							mid_x: mid_x ,
							mid_y: mid_y ,
						},{
							duration: 35,
							name: 'hud_item_carousel',
							replace_existing: true,
							easing: lib.math.ease_in_sine
					})
				]
			)
		",
		

#-------------------------- constructor/destructor --------------------------#
		constructor: "[
			map(graphic_cells, add_object(value)),
			map(graphic_cells, [
				set(value.mid_x, mid_x),
				set(value.mid_y, mid_y),
			]),
			add_object(overlay),
			set(overlay.parent, graphic)
			;
			set(overlay.relative_x, 0),
			set(overlay.relative_y, 0),
			set(me.alpha, 0),
		]"
	},
	
	on_start_level: "constructor", //When we enter a new level, the frogatto object retains the reference to ability_display object. Frogatto takes care of adding this object to the level. However, this object must add any objects it added previously. Since the object is only created once, on_create only ever fires once. Since the first level is not entered when this object is around, on_start_level only fires every time after the first level we enter.
	
	on_create: "constructor",
	
	//Sync UI state with Frogatto.
	on_process: "
		if(player is obj frogatto_playable,
			[
				map(graphic_cells,
					set_to_animation(value, item_info(index).hud_img_rect )
				),
				
				set(graphic.brightness, 255/if(mana.ready, 1, 3)),

				if(has_just_changed,
					do_carousel_anim
				),				
			]
				where mana = player.current_mana_availability
				where item_info = (def(int offset) -> class inventory_item
					(if(normalized_index = 0, 
						player.item_info(player.current_secondary_item),
						player.item_info( player.nth_item_away_from_current_secondary_item(normalized_index) )
					)) where normalized_index = offset - 2 )
		)
	",
	
	animation: {
		id: "bg", 
		image: "gui/hud3.png",
		rect: [80,0,127,47],
	},
	
	object_type: [
		{
			id: "graphic",
			is_strict: true,
			use_absolute_screen_coordinates: true,
			serializable: false,
			zorder: "@include data/zorder.cfg:in_front_of_everything",
			zsub_order: -2,
			
			animation: [{
				"@base": true,
				image: "gui/hud4.png",
			}],
		},

		{
			id: "overlay",
			is_strict: true,
			use_absolute_screen_coordinates: true,
			serializable: false,
			zorder: "@include data/zorder.cfg:in_front_of_everything",
			zsub_order: 0,
			
			animation: [{
				id: "normal",
				image: "gui/hud3.png",
				rect: [80,0,127,47],
			}],
		},
		
	],
	
	
}