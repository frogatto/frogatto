{
id: "crumbling_bricks",
activation_area: 100,
is_strict: true,
prototype: ["cosmetic_effects"],
zorder: "@include data/zorder.cfg:interior_decor",
platform_area: [16,16,31,17],
hitpoints: 300,
properties: {
	crumble_per_cycle: "if(higher_difficulty, 10, 2)",
	pick_animation: "animation('health'+((hitpoints/100)+1))",
	material_sound: "enum stone",
},
on_create: "[animation('health4'),set(facing,if(1d2=2,1,-1))]",
on_process: "if(level.player.standing_on = me,
				[
					add(hitpoints,-crumble_per_cycle),
					pick_animation,
					if(cycle%5=0 and 1d2=2,
						[
							spawn('bouncing_debris_chunk', mid_x, mid_y + (1d10), {facing: if(1d2=2,1,-1), debris_type: enum rock_big_cement, init_vel: enum fall_down} ),
							sound('Rock-Crack'+1d6+'.wav')
						]
					)
				]
			)",

on_die: "[
			sound('blockbreak.ogg'),
			map(range(3d2), spawn('bouncing_debris_chunk', mid_x, mid_y + (1d10), {facing: if(1d2=2,1,-1), debris_type: enum rock_small_cement, init_vel: enum burst} )),
			map(range(1d2), spawn('bouncing_debris_chunk', mid_x, mid_y + (1d10), {facing: if(1d2=2,1,-1), debris_type: enum rock_big_cement, init_vel: enum burst} )),
		]",


editor_info: {
	category: "props",
},
animation: [
	{
		"@base": true,
		image: "props/crumbling-bricks-grey-rock.png",
		duration: 10000000,
		feet_x: 24,
		feet_y: 32,
		palettes: "@include data/palettes.cfg: seaside_palettes + forest_palettes + cave_palettes + dungeon_palettes",
	},		
	{
		id: "health4",
		rect: [0,0,47,47],
	},
	{
		id: "health3",
		rect: [48,0,95,47],
	},
	{
		id: "health2",
		rect: [96,0,143,47],
	},
	{
		id: "health1",
		rect: [144,0,191,47],
	},
	{
		id: "health0",
		rect: [144,0,191,47],
	},
],
}