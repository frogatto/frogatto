# Level controllers are generic objects designed to be used as triggers for scripts.
# Various usages include modifying:
#  - on_create to trigger something on the start of a level.
#  - on_triggered to trigger something when the player reaches a certain spot
{
id: "heightmap_platform_controller",
next_animation: "'normal'",
always_active: true,
is_strict: true,
hidden_in_game: true,
platform_area: [0,0,10],

properties: {
	material_sound: "self.footfall_sound_effect",
#-------------------------- vars --------------------------#
	heightmap: { type: "[[int,int]]", default: [], editor_info: { type: "points", value: "[]" } },
	footfall_sound_effect: { type: "string", editor_info: { type: "enum", enum_values: ["wood", "foliage", "dirt", "stone", "wood_solid", "metal", "padding"], value: "metal" } },
	
	first_height: "[int,int] :: heightmap[0] asserting size(heightmap) > 0",
	last_height: "[int,int] :: heightmap[size(heightmap)-1] asserting size(heightmap) > 0",
	back_of_heightmap: "([int,int] <- back(heightmap)) asserting size(heightmap) > 0",
	head_of_heightmap: "([int,int] <- head(heightmap)) asserting size(heightmap) > 0",
},

on_create: "if(size(heightmap) >= 2,
   [set(platform_area, [(first_height[0] - x)/2, 0, (last_height[0] - x)/2]),
    set(platform_offsets, map(heightmap, value[1] - y))
   ],
   set(platform_area, null),
   )",

on_editor_changed_variable: "if(size(heightmap) >= 2,

 if(head_of_heightmap[0] > back_of_heightmap[0]-10,
 #don't let the left side cross over the right#
 [set(heightmap,
    [[back_of_heightmap[0]-10, first_height[1]]] +
    heightmap[1:]),
  fire_event('editor_changed_variable')],

 #adjust the vertexes so they are evenly spaced on the x axis#
 [set(heightmap, map(heightmap, [
     (first_height[0]*r + last_height[0]*(1000-r))/1000, value[1]]
       where r = (1000*((size(heightmap)-1) - index))/
                        (size(heightmap)-1))),
  fire_event('create')
 ])
)",

on_end_anim: "animation('normal')",
zorder: "@include data/zorder.cfg:in_front_of_everything",
editor_info: {
	category: "controllers",
},
animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 86,
	y: 73,
	w: 28,
	h: 28,
	frames: 1,
	duration: 1000,
},
}
