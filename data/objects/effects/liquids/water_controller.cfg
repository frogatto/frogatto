{
id: "water_controller",
prototype: ["liquid"],
zorder: "@include data/zorder.cfg:liquid_particle_effects",

properties: {
	liquid_texture: "string :: 'liquids/water-texture2.png'",
	texture_speed: "decimal :: 0.002",

	add_water_effect: "add_water(_x_bound, _y_bound, _x2_bound, _y2_bound)",
	


#-------------------------- fx objects --------------------------#
	volume_bubbles: { 
		type: "obj particle_attache_slicer", 
		init: "object('particle_attache_slicer', x,y, {
			_particle_type:'bubbles_2', //Silence check for this existing. `particles`, which this sets, is overwritten later.
			parent: me,
			time_to_die: 0,
			x:x, y:y,
			zorder: lib.zorder.get('liquid_particle_effects'),
			child_system_count: child_system_count,
			slice_width: slice_width,
			water_width: water_width,
			water_height: water_height,
			water_boundaries: water_boundaries,
		})",
		persistent: false,
	},

	underlay_obj: {
		type: "obj water_controller.underlay",
		init: "object('water_controller.underlay', mid_x, mid_y, { 
				parent: me,
				time_to_die: 50,
			})",
		persistent: false,
	},
	top_obj: {
		type: "obj water_controller.top",
		init: "object('water_controller.top', mid_x, mid_y, {
				parent: me,
				time_to_die: 50,
			})",
		persistent: false,
	},
	lights_obj: {
		type: "obj water_controller.lights",
		init: "object('water_controller.lights', mid_x, mid_y, {
				parent: me,
				time_to_die: 50,
			})",
		persistent: false,
	},
	lights_transverse_obj: {
		type: "obj water_controller.lights",
		init: "object('water_controller.lights', mid_x, mid_y, {
				parent: me,
				time_to_die: 50,
				_timeshift: 1.4,
			})",
		persistent: false,
	},



#-------------------------- constructors --------------------------#
	liquid_obj_constructor: "commands :: [
		add_particle_effect ;
		setup_particles ;
		[
			setup_shader,
			add_object(underlay_obj),
			add_object(top_obj),
			add_object(lights_obj),
			add_object(lights_transverse_obj),
		]

	]",

	add_particle_effect: "[
		add_object(volume_bubbles),
	]",

	setup_particles: "[
		map(
			volume_bubbles.active_child_systems,
			set(value.particles,
				(
					raw_particle_system_info + {
						position: [
							0,
							_y2_bound - mid_y, //Position the center point (of the box emitter) at the bottom of the body of water.
							0
						],
						fast_forward: (map <- raw_particle_system_info.fast_forward) + {
							time: bubble_average_lifetime, //Pump the water full of bubbles when we first encounter it.
						},
						emitter: (map <- raw_particle_system_info.emitter) + {
							time_to_live: bubble_average_lifetime, //Make the bubbles live until the top of the water.
							box_width: slice_width, //Make the bubbles as wide as the water. (To position the bubbles, see `position` above.)
							emission_rate: (map <- (map <- raw_particle_system_info.emitter).emission_rate) + {
								max: ((_x2_bound - _x_bound) / 20) / slice_count, //Particle density. (Particles per pixel of water surface, divided by 2 on average because random floor is 0.)
							},
						},
					} where
						raw_particle_system_info = 
							(ParticleSystemInfo <- get_document('data/particles/bubbles_2.cfg')).info,
						bubble_average_lifetime = 
							(_y2_bound - _y_bound) / average_vertical_bubble_speed / 60/*fps*/
							where average_vertical_bubble_speed = 0.65 //as experimentally verified ([DDR 2020-40-24] 0.6 is actually more proper, but I like the bubbles disappearing before they hit the surface sometimes.)
				)
			)
		),
	]",
},


on_editor_changed_variable: "[
	//Hack: Fix water not moving in response to editor. (I think it gets set to the original object, not the 'shadow object' we're editing.)
	
	proto_event('liquid', 'editor_changed_variable'),
]",


animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 117, y: 42, w: 28, h: 28,
	frames: 1,
	duration: 1000,
},



object_type: [
	{
		id: "underlay",
		prototype: ["effects_attache"],
		zorder: "@include data/zorder.cfg:background_parallax_walls_further",
		properties: {

			do_setup: "[
				set(x,_parent_obj._x_bound),
				set(y,_parent_obj._y_bound),
				set(draw_area, [
					0,
					_offset, 
					abs(_parent_obj._x2_bound-_parent_obj._x_bound)/2, 
					abs(_parent_obj._y2_bound-_parent_obj._y_bound)/2
				]),
			] asserting _parent_obj is obj liquid",
			
			handle_specialized_processing: "[
				add(_offset,0.25),
				do_setup,
			]",

		#-------------------------- vars --------------------------#
			_offset: { type: "decimal", default: 0, persistent: false },
		},

		#we don't want to compile the image since we rely on it tiling.
		no_compile_image: "liquids/water-texture.png",
		animation: {
			image: "liquids/water-texture.png",
			id: "normal",
			x: 0,
			w: 256,
			y: 0,
			h: 256,
		},
	},
	{
		id: "top",
		prototype: ["effects_attache"],
		zorder: "@include data/zorder.cfg:exterior_near_background",
		properties: {
			
			do_setup: "[
				set(x, _parent_obj._x_bound),
				set(y, _parent_obj._y_bound - 20),
				set(draw_area, [
					5.0*sin(_offset), 
					0, 
					abs(_parent_obj._x2_bound-_parent_obj._x_bound)/2, 
					img_h/2
				])
			] asserting _parent_obj is obj liquid",
			
			handle_specialized_processing: "[
				add(_offset,1.5),
				do_setup,
			]",


		#-------------------------- vars --------------------------#
			_offset: { type: "decimal", default: 0, persistent: false },
		},


		#we don't want to compile the image since we rely on it tiling.
		no_compile_image: "liquids/water-top.png",
		animation: {
			image: "liquids/water-top.png",
			id: "normal",
			x: 0,
			w: 128,
			y: 0,
			h: 16,
		},
	},
	{
		id: "lights",
		prototype: ["effects_attache"],
		zorder: "@include data/zorder.cfg:exterior_near_background",
		zsub_order: 1,
		properties: {

			do_setup: "[
				set(alpha,50),

				set(x,_parent_obj._x_bound),
				set(y,_parent_obj._y_bound),
				set(draw_area, [
					5.0*_timeshift*sin(_offset), 
					0, 
					abs(_parent_obj._x2_bound-_parent_obj._x_bound)/2, 
					img_h/2
				]),
			] asserting _parent_obj is obj liquid",
			
			handle_specialized_processing: "[
				add(_offset,1.5),
				do_setup,
			]",


		#-------------------------- vars --------------------------#
			_offset: { type: "decimal", default: 0, persistent: false },
			_timeshift: { type: "decimal", default: 0, persistent: false },
		},


		#we don't want to compile the image since we rely on it tiling.
		no_compile_image: "liquids/water-texture-lights.png",
		animation: {
			image: "liquids/water-texture-lights.png",
			blend:["src_alpha","one"],
			id: "normal",
			x: 0,
			w: 256,
			y: 0,
			h: 128,
		},
	},
],

}
